#CSR = #sparse_tensor.encoding<{ map = (d0, d1) -> (d0: dense, d1: compressed) }>

module {

  func.func @spmv(%mat: tensor<{{ rows }}x{{ cols }}xf64, #CSR>,
                  %vec: tensor<{{ cols }}xf64>,
		          %res: tensor<{{ rows }}xf64>) -> tensor<{{ rows }}xf64> {

    %rows = arith.constant {{ rows }} : index
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %pd = arith.constant {{ pd }} : index

    %positions_buff = sparse_tensor.positions %mat {level = 1 : index} : tensor<{{ rows }}x{{ cols }}xf64, #CSR> to memref<?xindex>
    %coordinates_buff = sparse_tensor.coordinates %mat {level = 1 : index} : tensor<{{ rows }}x{{ cols }}xf64, #CSR> to memref<?xindex>
    %values_buff = sparse_tensor.values %mat : tensor<{{ rows }}x{{ cols }}xf64, #CSR> to memref<?xf64>

    %vec_buff = bufferization.to_memref %vec : memref<{{ cols }}xf64>
    %res_buff = bufferization.to_memref %res : memref<{{ rows }}xf64>

    scf.for %i = %c0 to %rows step %c1 {
      %f0 = memref.load %res_buff[%i] : memref<{{ rows }}xf64>

      %j_start = memref.load %positions_buff[%i] : memref<?xindex>
      %i_plus_one = arith.addi %i, %c1 : index
      %j_end = memref.load %positions_buff[%i_plus_one] : memref<?xindex>

      %res_i = scf.for %j = %j_start to %j_end step %pd iter_args(%acc1 = %f0) -> f64 {

        // find min(j + pd, j_end)
        %j_plus_pd = arith.addi %j, %pd : index
        %j_plus_pd_or_j_end = arith.minui %j_plus_pd, %j_end : index

        // SPE loop
        scf.for %pre = %j to %j_plus_pd_or_j_end step %c1 {
          %col_idx = memref.load %coordinates_buff[%pre] : memref<?xindex>
          memref.prefetch %vec_buff[%col_idx], read, locality<{{ loc_hint }}>, data : memref<{{ cols }}xf64>
        }

        // main loop
        %partial_dot_prod = scf.for %main = %j to %j_plus_pd_or_j_end step %c1 iter_args(%acc2 = %f0) -> f64 {
          %col_idx = memref.load %coordinates_buff[%main] : memref<?xindex>
          %t_val = memref.load %values_buff[%main] : memref<?xf64>
          %v_val = memref.load %vec_buff[%col_idx] : memref<{{ cols }}xf64>
          %14 = arith.mulf %t_val, %v_val : f64
          %15 = arith.addf %acc2, %14 : f64
          scf.yield %15 : f64
        }

        %dot_prod = arith.addf %acc1, %partial_dot_prod : f64
        scf.yield %dot_prod : f64
      }

      memref.store %res_i, %res_buff[%i] : memref<{{ rows }}xf64>
    }

    %5 = bufferization.to_tensor %res_buff restrict : memref<{{ rows }}xf64>
    return %5 : tensor<{{ rows }}xf64>
  }
}
